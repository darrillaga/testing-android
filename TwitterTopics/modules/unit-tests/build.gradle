evaluationDependsOn(':app')
apply plugin: 'java'
apply plugin: 'jacoco'
sourceCompatibility = 1.7
version = '1.0'

final JUNIT_VERSION = '4.11'
final JUNIT_HIERARCHICAL_CONTEXT_RUNNER = '4.11.3'
final MOCKITO_VERSION = '1.9.5'
final HAMCREST_VERSION = '1.3'

def androidSDKPath =  "$System.env.ANDROID_HOME"
repositories {
    mavenCentral()
    maven { url 'file://' + androidSDKPath + '/extras/android/m2repository' }
    maven { url 'file://' + androidSDKPath + '/extras/google/m2repository' }
}

dependencies {
    def androidModule = project(':app')

    //add our app classes to compilation path
    compile androidModule

    //add all of the jars and output from our 'first' variant to the test classpath
    def compileTask = androidModule.android.applicationVariants.toList().first().javaCompile
    testCompile compileTask.classpath
    testCompile compileTask.outputs.files

    testCompile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile group: 'junit', name: 'junit', version: JUNIT_VERSION

    testCompile group: 'de.bechte.junit', name: 'junit-hierarchicalcontextrunner',
            version: JUNIT_HIERARCHICAL_CONTEXT_RUNNER

    testCompile group: 'org.mockito', name: 'mockito-all', version: MOCKITO_VERSION

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: HAMCREST_VERSION
}